{
 "cells": [
  {
   "metadata": {
    "collapsed": true
   },
   "cell_type": "markdown",
   "source": [
    "# 5.i. BDR-specific querying\n",
    "\n",
    "Int his module we will test SPARQL queries on the Biodiversity Data Repository (BDR) system.\n",
    "\n",
    "Before training with this module, a revision of Modules 3 & 4 should be done first.\n",
    "\n",
    "### Data Access\n",
    "\n",
    "This module requires that queries be executed on a SPARQL Endpoint with access to BDR data. While public access to some demo BDR data is available, there may not be public access to the cull BDR content.\n",
    "\n",
    "> **NOTE**: A BDR full content endpoint will be provisioned for this work, but it will not be recorded here for general, public, access."
   ],
   "id": "4fb08c221a688777"
  },
  {
   "metadata": {},
   "cell_type": "markdown",
   "source": [
    "## Outline\n",
    "\n",
    "1. Test access as usual\n",
    "2. Approaching a DB\n",
    "    * generic approaches to discovering what's in any large RDF DB\n",
    "2. Approaching the BDR via ABIS / BDR PR\n",
    "3. BDR Queries\n",
    "    * partly prepared by Ashley\n",
    "4. Desktop GIS SPARQL Use"
   ],
   "id": "24d24154d1679054"
  },
  {
   "metadata": {},
   "cell_type": "markdown",
   "source": "## 5.i.0 Test access as usual",
   "id": "566eba279e6c468f"
  },
  {
   "metadata": {},
   "cell_type": "code",
   "source": [
    "# import some Python packages\n",
    "from kurra.sparql import query\n",
    "from IPython.display import display, Markdown\n",
    "from kurra.utils import render_sparql_result\n",
    "\n",
    "# define our pretty-print function\n",
    "def table_print(r):\n",
    "    display(Markdown(render_sparql_result(r)))"
   ],
   "id": "1c326c455c43ece9",
   "outputs": [],
   "execution_count": null
  },
  {
   "metadata": {},
   "cell_type": "code",
   "source": [
    "bdr_sparql_endpoint = \"XXXX\"\n",
    "\n",
    "# make a simple SPARQL query\n",
    "q = \"\"\"\n",
    "PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>\n",
    "PREFIX owl: <http://www.w3.org/2002/07/owl#>\n",
    "PREFIX skos: <http://www.w3.org/2004/02/skos/core#>\n",
    "\n",
    "SELECT *\n",
    "WHERE {\n",
    "  ?o a owl:Ontology .\n",
    "\n",
    "  OPTIONAL {\n",
    "    ?o rdf:label|skos:prefLabel ?label\n",
    "  }\n",
    "}\n",
    "\"\"\"\n",
    "\n",
    "r = query(bdr_sparql_endpoint, q)\n",
    "table_print(r)"
   ],
   "id": "517e9c5485dd2dac",
   "outputs": [],
   "execution_count": null
  },
  {
   "metadata": {},
   "cell_type": "code",
   "source": [
    "# Force a timeout, maybe...?\n",
    "q = \"\"\"\n",
    "PREFIX skos: <http://www.w3.org/2004/02/skos/core#>\n",
    "\n",
    "SELECT *\n",
    "WHERE {\n",
    "  ?o a skos:Concept .\n",
    "\n",
    "  OPTIONAL {\n",
    "    ?o skos:prefLabel ?label\n",
    "  }\n",
    "}\n",
    "\"\"\"\n",
    "\n",
    "q2 = \"\"\"\n",
    "PREFIX schema: <https://schema.org/>\n",
    "\n",
    "SELECT *\n",
    "WHERE {\n",
    "  ?o a schema:Dataset\n",
    "\n",
    "  OPTIONAL {\n",
    "    ?o schema:name ?label\n",
    "  }\n",
    "}\n",
    "ORDER BY DESC(?label)\n",
    "\"\"\"\n",
    "\n",
    "r = query(bdr_sparql_endpoint, q)\n",
    "table_print(r)  # 115,868 Concepts, 10k+ Datasets"
   ],
   "id": "d9fa9237058d8ff7",
   "outputs": [],
   "execution_count": null
  },
  {
   "metadata": {},
   "cell_type": "code",
   "source": [
    "# Respect the timeout\n",
    "q = \"\"\"\n",
    "PREFIX skos: <http://www.w3.org/2004/02/skos/core#>\n",
    "\n",
    "SELECT *\n",
    "WHERE {\n",
    "  ?o a skos:Concept .\n",
    "\n",
    "  OPTIONAL {\n",
    "    ?o skos:prefLabel ?label\n",
    "  }\n",
    "}\n",
    "LIMIT 10\n",
    "\"\"\"\n",
    "\n",
    "r = query(bdr_sparql_endpoint, q)\n",
    "table_print(r)"
   ],
   "id": "f5f1fd4b16932531",
   "outputs": [],
   "execution_count": null
  },
  {
   "metadata": {},
   "cell_type": "markdown",
   "source": [
    "### 5.i.1 Approaching a DB\n",
    "\n",
    "#### Look for definitional items\n",
    "\n",
    "* Search for any `owl:Ontology` instances\n",
    "* Search for any `skos:ConceptScheme` instances\n",
    "\n",
    "#### View the BD structure\n",
    "\n",
    "* Search for Named Graphs\n",
    "\n",
    "```sparql\n",
    "SELECT DISTINCT ?g\n",
    "WHERE {\n",
    "  GRAPH ?g {\n",
    "   \t?s ?p ?o\n",
    "  }\n",
    "}\n",
    "LIMIT 100\n",
    "```\n",
    "\n",
    "Then try to `DESCRIBE` a graph."
   ],
   "id": "3a35e98d24be2bdd"
  },
  {
   "metadata": {},
   "cell_type": "markdown",
   "source": [
    "### 5.i.2 Approaching the BDR via ABIS / BDR PR\n",
    "\n",
    "#### Read the documentation\n",
    "\n",
    "In this case, start at the [BDR Resources Page](https://resources.bdr.gov.au/)\n",
    "\n",
    "* [ABIS](https://linked.data.gov.au/def/abis)\n",
    "* [BDR Profile of ABIS](https://linked.data.gov.au/def/bdr-pr)\n",
    "* [BDR Catalogues](https://resources.bdr.gov.au/catalogues)\n",
    "\n",
    "#### Ask someone\n",
    "\n",
    "The BDR Team!"
   ],
   "id": "2d18f99d826d06d2"
  },
  {
   "metadata": {},
   "cell_type": "markdown",
   "source": [
    "### 5.i.3 BDR Queries\n",
    "\n",
    "#### Count Occurrences\n",
    "\n",
    "```sparql\n",
    "SELECT (COUNT(?o) AS ?count)\n",
    "WHERE {\n",
    "\t?o a <http://rs.tdwg.org/dwc/terms/Occurrence>\n",
    "}\n",
    "```\n",
    "\n",
    "#### Count occurrences in a Dataset\n",
    "\n",
    "![](Dataset_Records.svg)\n",
    "\n",
    "```sparql\n",
    "PREFIX abis: <https://linked.data.gov.au/def/abis/>\n",
    "PREFIX schema: <https://schema.org/>\n",
    "SELECT ?dataset (COUNT(?rec) as ?record_count) {\n",
    "    BIND (<https://linked.data.gov.au/dataset/bdr/63c890eb-cd66-4cb5-8518-45bd6dc1409c> as ?ds)\n",
    "    ?dataset a schema:Dataset .\n",
    "    ?rec a abis:BiodiversityRecord ;\n",
    "      schema:isPartOf ?dataset .\n",
    "} GROUP BY ?dataset\n",
    "```\n",
    "\n",
    "#### Find `Dataset` instances, and some metadata\n",
    "\n",
    "```sparql\n",
    "PREFIX prov: <http://www.w3.org/ns/prov#>\n",
    "PREFIX schema: <https://schema.org/>\n",
    "SELECT ?dataset ?ds_name ?provider ?provider_name WHERE {\n",
    "    ?dataset a schema:Dataset .\n",
    "    ?dataset schema:name ?ds_name ;\n",
    "      prov:qualifiedAttribution [\n",
    "        prov:hadRole <https://linked.data.gov.au/def/data-roles/resourceProvider> ;\n",
    "        prov:agent ?provider\n",
    "      ] .\n",
    "    ?provider schema:name ?provider_name .\n",
    "} LIMIT 1000 # Limit because there are over 10,000 ds\n",
    "```\n",
    "\n",
    "#### Count the records\n",
    "\n",
    "In every Dataset provided by QLD WildNet\n",
    "\n",
    "(Find the qld wildnet URI by looking in the BDR Orgs Catalogue!)\n",
    "\n",
    "```sparql\n",
    "PREFIX abis: <https://linked.data.gov.au/def/abis/>\n",
    "PREFIX prov: <http://www.w3.org/ns/prov#>\n",
    "PREFIX schema: <https://schema.org/>\n",
    "SELECT ?dataset (COUNT(?rec) as ?record_count) WHERE {\n",
    "    ?dataset a schema:Dataset ;\n",
    "      prov:qualifiedAttribution [\n",
    "        prov:hadRole <https://linked.data.gov.au/def/data-roles/resourceProvider> ;\n",
    "        prov:agent <https://linked.data.gov.au/org/qld-wildnet>\n",
    "      ] .\n",
    "    ?rec a abis:BiodiversityRecord .\n",
    "    ?rec schema:isPartOf ?dataset .\n",
    "} GROUP BY ?dataset\n",
    "LIMIT 1000 # Must limit to 1000 because counting in graphs is SLOW!\n",
    "```\n",
    "\n",
    "#### Coarse Geospatial search.\n",
    "\n",
    "Find which Datasets bounding boxes intersect the bounding box of K'gari\n",
    "\n",
    "![](kgari_box.png)\n",
    "\n",
    "```sparql\n",
    "PREFIX geof: <http://www.opengis.net/def/function/geosparql/>\n",
    "PREFIX schema: <https://schema.org/>\n",
    "PREFIX geo: <http://www.opengis.net/ont/geosparql#>\n",
    "\n",
    "SELECT ?dataset WHERE {\n",
    "    BIND(\"\"\"POLYGON ((152.935181 -25.787527, 153.382874 -25.787527, 153.382874 -24.684457, 152.935181 -24.684457, 152.935181 -25.787527))\"\"\"^^geo:wktLiteral as ?Kgari_bbox)\n",
    "    ?dataset a schema:Dataset .\n",
    "    ?dataset geo:hasBoundingBox ?geo .\n",
    "    ?geo geo:asWKT ?bounds\n",
    "    FILTER geof:sfIntersects(?bounds, ?Kgari_bbox)\n",
    "}\n",
    "LIMIT 1000\n",
    "```\n",
    "\n",
    "#### Finer Geospatial search\n",
    "\n",
    "Find which Dataasets (using their convex hull) contain occurrences on K'gari polygon\n",
    "\n",
    "![](kgari_hull.png)\n",
    "\n",
    "```sparql\n",
    "PREFIX geof: <http://www.opengis.net/def/function/geosparql/>\n",
    "PREFIX schema: <https://schema.org/>\n",
    "PREFIX geo: <http://www.opengis.net/ont/geosparql#>\n",
    "\n",
    "SELECT ?dataset WHERE {\n",
    "    BIND(\"\"\"POLYGON ((153.231812 -24.666986, 153.045044 -24.791722, 153.198853 -24.96614, 152.946167 -25.207426, 153.014832 -25.34899, 152.896729 -25.532528, 152.946167 -25.703413, 152.987366 -25.79, 153.099976 -25.819672, 153.388367 -25.000994, 153.316956 -24.66449, 153.231812 -24.666986))\"\"\"^^geo:wktLiteral as ?Kgari_polygon)\n",
    "    ?dataset a schema:Dataset .\n",
    "    ?dataset geo:hasGeometry ?geo .\n",
    "    ?geo geo:asWKT ?bounds\n",
    "    FILTER geof:sfIntersects(?bounds, ?Kgari_polygon)\n",
    "}\n",
    "LIMIT 100\n",
    "```\n",
    "\n",
    "### National Species List Use\n",
    "\n",
    "NSL relationships in the BDR (This is using the official published RDF version of the NSL)\n",
    "\n",
    "<div>\n",
    "    <img src=\"module-5i-files/NSL_Toplevel.svg\" width=\"75%\"/>\n",
    "</div>\n",
    "\n",
    "\n",
    "#### Find which datasets containing Species\n",
    "\n",
    "_Velleia perfoliata R.Br._\n",
    "\n",
    "![](NameUsageSubject.svg)\n",
    "\n",
    "```sparql\n",
    "PREFIX schema: <https://schema.org/>\n",
    "PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>\n",
    "PREFIX nsl: <https://linked.data.gov.au/def/nsl/>\n",
    "SELECT ?dataset\n",
    "WHERE {\n",
    "    {\n",
    "        SELECT ?nsl_usage\n",
    "        WHERE {\n",
    "            GRAPH <https://biodiversity.org.au/nsl/2025-06-16> {\n",
    "                ?nsl_name\n",
    "                    a nsl:TaxonName ;\n",
    "                    rdfs:label \"Velleia perfoliata R.Br.\" ;\n",
    "                .\n",
    "\n",
    "                ?nsl_usage nsl:usageOf ?nsl_name .\n",
    "            }\n",
    "        }\n",
    "    }\n",
    "\n",
    "    ?dataset a schema:Dataset .\n",
    "\n",
    "    ?nsl_usage\n",
    "        schema:subjectOf ?dataset ;\n",
    "    .\n",
    "}\n",
    "LIMIT 100\n",
    "```\n",
    "\n",
    "\n",
    "#### Find location and time of all instances of Species\n",
    "\n",
    "_Velleia perfoliata R.Br._,  within one given Dataset:\n",
    "\n",
    "```sparql\n",
    "PREFIX time: <http://www.w3.org/2006/time#>\n",
    "PREFIX geo: <http://www.opengis.net/ont/geosparql#>\n",
    "PREFIX schema: <https://schema.org/>\n",
    "PREFIX abis: <https://linked.data.gov.au/def/abis/>\n",
    "PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>\n",
    "PREFIX nsl: <https://linked.data.gov.au/def/nsl/>\n",
    "SELECT ?record ?id ?occurrence ?location ?datetime\n",
    "WHERE {\n",
    "    {\n",
    "        SELECT ?nsl_usage\n",
    "        WHERE {\n",
    "            GRAPH <https://biodiversity.org.au/nsl/2025-06-16> {\n",
    "                ?nsl_name\n",
    "                    a nsl:TaxonName ;\n",
    "                    rdfs:label \"Velleia perfoliata R.Br.\" ;\n",
    "                .\n",
    "\n",
    "                ?nsl_usage nsl:usageOf ?nsl_name .\n",
    "            }\n",
    "        }\n",
    "    }\n",
    "\n",
    "    BIND (<https://linked.data.gov.au/dataset/bdr/c1130d9b-2655-4454-bab1-9c3cebe381dc> as ?dataset)\n",
    "\n",
    "    ?record\n",
    "        a abis:BiodiversityRecord ;\n",
    "        schema:identifier ?id ;\n",
    "        schema:isPartOf ?dataset ;\n",
    "        schema:about ?occurrence ;\n",
    "    .\n",
    "    ?nsl_usage schema:subjectOf ?occurrence .\n",
    "    ?occurrence\n",
    "        schema:spatial ?spatial ;\n",
    "        schema:temporal ?temporal ;\n",
    "    .\n",
    "\n",
    "    ?spatial geo:asWKT ?location .\n",
    "\n",
    "    ?temporal time:inXSDDate|time:inXSDDateTime|time:inXSDDateTimeStamp|time:inXSDgYear|time:inXSDgYearMonth ?datetime .\n",
    "}\n",
    "LIMIT 100\n",
    "```\n",
    "\n",
    "#### Show all observations on a given occurrence\n",
    "\n",
    "(this shows the values of observable properties at a given time)\n",
    "\n",
    "![](ObservationsObservationCollections.svg)\n",
    "\n",
    "```sparql\n",
    "PREFIX geo: <http://www.opengis.net/ont/geosparql#>\n",
    "PREFIX time: <http://www.w3.org/2006/time#>\n",
    "PREFIX sosa: <http://www.w3.org/ns/sosa/>\n",
    "PREFIX schema: <https://schema.org/>\n",
    "PREFIX skos: <http://www.w3.org/2004/02/skos/core#>\n",
    "SELECT ?record ?observation ?property ?property_label ?procedure ?procedure_label ?result ?time_val ?time_wkt ?comment\n",
    "WHERE {\n",
    "    BIND (<https://linked.data.gov.au/dataset/bdr/c1130d9b-2655-4454-bab1-9c3cebe381dc/biodiversityRecord/7768> as ?record)\n",
    "\n",
    "    ?record schema:about ?occurrence .\n",
    "\n",
    "    ?observation\n",
    "        sosa:hasFeatureOfInterest ?occurrence ;\n",
    "        sosa:observedProperty ?property ;\n",
    "        sosa:usedProcedure ?procedure ;\n",
    "        sosa:hasSimpleResult ?result ;\n",
    "    .\n",
    "\n",
    "    ?property skos:prefLabel ?property_label .\n",
    "\n",
    "    ?procedure skos:prefLabel ?procedure_label .\n",
    "\n",
    "    OPTIONAL {\n",
    "        ?observation schema:temporal|sosa:phenomenonTime ?time_node .\n",
    "        ?time_node time:inXSDDate|time:inXSDDateTime|time:inXSDDateTimeStamp|time:inXSDgYear|time:inXSDgYearMonth ?time_val .\n",
    "    }\n",
    "    OPTIONAL {\n",
    "        ?observation schema:spatial ?spatial_node .\n",
    "        ?spatial geo:asWKT ?spatial_wkt .\n",
    "    }\n",
    "    OPTIONAL {\n",
    "        ?observation schema:comment ?comment .\n",
    "    }\n",
    "}\n",
    "LIMIT 10\n",
    "```\n",
    "\n",
    "#### Get all the Attributes\n",
    "\n",
    "Using `tern:hasAttribute`, for an Occurrence, or attributes of the Occurrence the record is about.\n",
    "\n",
    "```sparql\n",
    "PREFIX tern: <https://w3id.org/tern/ontologies/tern/>\n",
    "PREFIX geo: <http://www.opengis.net/ont/geosparql#>\n",
    "PREFIX time: <http://www.w3.org/2006/time#>\n",
    "PREFIX sosa: <http://www.w3.org/ns/sosa/>\n",
    "PREFIX schema: <https://schema.org/>\n",
    "PREFIX skos: <http://www.w3.org/2004/02/skos/core#>\n",
    "\n",
    "SELECT ?record ?attr ?attr_label ?val\n",
    "WHERE {\n",
    "    BIND (<https://linked.data.gov.au/dataset/bdr/c1130d9b-2655-4454-bab1-9c3cebe381dc/biodiversityRecord/7768> as ?record)\n",
    "\n",
    "    ?record schema:about ?occurrence .\n",
    "    {\n",
    "        ?occurrence tern:hasAttribute ?attr_node .\n",
    "    }\n",
    "    UNION\n",
    "    {\n",
    "        ?record tern:hasAttribute ?attr_node .\n",
    "    }\n",
    "    UNION\n",
    "    {\n",
    "        ?attr_collection\n",
    "            tern:hasAttribute ?attr_node ;\n",
    "            schema:hasPart ?record ;\n",
    "        .\n",
    "    }\n",
    "    UNION\n",
    "    {\n",
    "        ?attr_collection\n",
    "            tern:hasAttribute ?attr_node ;\n",
    "            schema:hasPart ?occurrence ;\n",
    "        .\n",
    "    }\n",
    "\n",
    "    ?attr_node\n",
    "        tern:attribute ?attr ;\n",
    "        tern:hasSimpleValue ?val ;\n",
    "    .\n",
    "\n",
    "    OPTIONAL {\n",
    "        ?attr skos:prefLabel ?attr_label .\n",
    "    }\n",
    "}\n",
    "LIMIT 10\n",
    "```"
   ],
   "id": "1ecb7bfdbc7cede7"
  },
  {
   "metadata": {},
   "cell_type": "markdown",
   "source": [
    "## 5.i.4 Desktop GIS SPARQL Use\n",
    "\n",
    "Using QGIS & SPARQLing Unicorn\n",
    "\n",
    "[QGIS](https://qgis.org/) is a free and open-source desktop GIS environment, similar to ArcGIS Pro.\n",
    "\n",
    "The [SPARQLing Unicorn Plugin](https://plugins.qgis.org/plugins/sparqlunicorn/) is a QGIS Plugin that \"GeoJSON layer from SPARQL endpoint queries\".\n",
    "\n",
    "_we will do a manual demo_"
   ],
   "id": "4e39b712c84ecfbd"
  },
  {
   "metadata": {},
   "cell_type": "code",
   "source": "",
   "id": "f9d82f4600bf2198",
   "outputs": [],
   "execution_count": null
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 2
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython2",
   "version": "2.7.6"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
